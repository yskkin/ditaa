apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'pmd'
import org.apache.tools.ant.taskdefs.condition.Os

version = '0_9'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
  mavenCentral()
}

configurations {
  bundleJar
  compile.extendsFrom bundleJar
}

dependencies {
  bundleJar 'commons-cli:commons-cli:1.2'
  bundleJar 'net.htmlparser.jericho:jericho-html:3.1'
  compile 'batik:batik-bridge:1.6-1'
  compile files(javadocTools())

  testCompile 'junit:junit:4.11'
  testCompile 'com.github.stefanbirkner:system-rules:1.4.0'
}

sourceSets {
  main {
    java {
      srcDir 'src'
      exclude '**/test/**'
    }
    resources {
      srcDir 'src'
    }
  }
  test {
    java {
      srcDir 'src'
      include '**/test/**'
    }
    resources {
      srcDir rootDir
      include 'tests/**'
    }
  }
}

jar {
  manifest {
    attributes('Main-Class': 'org.stathissideris.ascii2image.core.CommandLineConverter')
  }
  configurations.bundleJar.each {
    from zipTree(it)
  }
}

tasks.withType(FindBugs) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

/*
 * Apple's JDK does not have tools.jar
 */
File javadocTools() {
  String javaHome = System.properties['java.home']
  File toolsJar = new File(javaHome, '../lib/tools.jar')
  toolsJar.exists() ? toolsJar : new File(javaHome, '../Classes/classes.jar')
}
